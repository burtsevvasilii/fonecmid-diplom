#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//@skip-check unknown-form-parameter-access
	Адрес = Параметры.Адрес;
	Данные = ПолучитьИзВременногоХранилища(Адрес);
	
	Диаграмма.Обновление = Ложь;
	Диаграмма.Очистить();
	
	ЗаполнитьГрафикОтпусков(Данные);
	
	Шкала = Диаграмма.ОбластьПостроения.ШкалаВремени.Элементы[0];
	Шкала.Метки.Очистить();
	
	МеткаВремени = Шкала.Метки.Добавить(НачалоДня(ТекущаяДатаСеанса()));
	МеткаВремени.Текст = "Текущая дата " + Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy");
	МеткаВремени.ЦветТекста = WebЦвета.Красный;				
	МеткаВремени.ЦветЛинии = WebЦвета.Красный;
	
	Диаграмма.Обновление = Истина;
	
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьГрафикОтпусков(Данные)
	
	Серия = Диаграмма.УстановитьСерию("Отпуска");
	Для Каждого Строка Из Данные Цикл
		Точка = Диаграмма.УстановитьТочку(Строка.Сотрудник);
		Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);;
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ДатаНачала;
		Интервал.Конец = Строка.ДатаОкончания;
		Если Строка.Более28Дней Тогда
			Значение.Цвет = WebЦвета.Красный;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти